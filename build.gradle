plugins {
  id 'com.github.johnrengelman.shadow' version '5.2.0'
}

apply plugin: 'scala'
apply plugin: 'application'

repositories {
    jcenter()
    maven {
        url 'https://mvnrepository.com/artifact'
    }
}

dependencies {
    implementation group: 'org.scala-lang' , name: 'scala-library'  , version: '2.13.4'
    implementation group: 'org.scala-lang' , name: 'scala-compiler' , version: '2.13.4'

    implementation group: 'org.scala-lang.modules', name: 'scala-xml_2.13', version: '1.3.0'

    // work-around for https://github.com/oracle/graal/issues/2761
    implementation group: 'org.scalameta', name: 'svm-subs_2.13', version: '20.2.0'
}

version = '1.6.4'

application {
    mainClassName = 'app.Scant'
}

processResources {
    duplicatesStrategy = DuplicatesStrategy.INCLUDE // allow duplicates
}

sourceSets {
    main {
        java {
            srcDir 'src/main/java'
        }
        resources {
            srcDir 'src/main/resources'
                exclude '**/*.sample'
        }
    }
}

task repl(type:JavaExec) {
    main = "scala.tools.nsc.MainGenericRunner"
    classpath = sourceSets.main.runtimeClasspath
    standardInput System.in
    args '-usejavacp'
}

shadowJar {
    classifier = ''
}

compileScala { // or: tasks.withType(ScalaCompile)
    scalaCompileOptions.additionalParameters = ["-feature","-deprecation","-unchecked","-Xlint","-Xfatal-warnings"]
}
